;cpmg
;avance-version (12/01/11)
;T2 measurement using Carr-Purcell-Meiboom-Gill sequence
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>


"p2=p1*2"
"d11=30m"


1 ze
2 d1
  p1 ph1
3 d20
  p2 ph2
  d20
  lo to 3 times c
  go=2 ph31
  d11 wr #0 if #0 ivc 
  lo to 1 times td1
exit


ph1=0 0 2 2 1 1 3 3 
ph2=1 3 1 3 0 2 0 2
ph31=0 0 2 2 1 1 3 3


;pl1 : f1 channel - power level for pulse (default)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                             [30 msec]
;d20: fixed echo time to allow elimination of diffusion
;     and J-mod. effects
;vc : variable loop counter, taken from vc-list
;ns: 8 * n
;ds: 16
;td1: number of experiments = number of values in vc-list

;define VCLIST

;this pulse program produces a ser-file (PARMOD = 2D)

;d20: d20 should be << 1/J ,but > (50 * P2)
;vc : vc should contain even numbers to provide 
;     for cancellation of 180 degree pulse errors



;$Id: cpmg,v 1.11 2012/01/31 17:49:22 ber Exp $
