;t2
;avance-version (12/01/11)
;T2 measurement
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>


"p2=p1*2"
"d11=30m"


"acqt0=-p1*2/3.1416"


1 ze
2 d1
  p1 ph1
  vd
  p2 ph2
  vd
  p2 ph2
  go=2 ph31
  d11 wr #0 if #0 ivd
  lo to 1 times td1
exit


ph1=0
ph2=0
ph31=0


;pl1 : f1 channel - power level for pulse (default)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                             [30 msec]
;vd : variable delay, taken from vd-list
;ns: 8 * n
;ds: 4
;td1: number of experiments = number of delays in vd-list
;FnMODE: undefined

;define VDLIST

;this pulse program produces a ser-file (PARMOD = 2D)